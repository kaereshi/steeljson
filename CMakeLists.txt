cmake_minimum_required(VERSION 3.8)

cmake_policy(VERSION 3.0)

project(steeljson VERSION 0.1.0 LANGUAGES CXX)

# project variables
set(STEELJSON_PACKAGE_NAME ${PROJECT_NAME})
set(STEELJSON_EXPORT_NAME ${STEELJSON_PACKAGE_NAME}-targets)
set(STEELJSON_CONFIG_FILENAME ${STEELJSON_PACKAGE_NAME}-config.cmake)
set(STEELJSON_CONFIGVERSION_FILENAME ${STEELJSON_PACKAGE_NAME}-config-version.cmake)
set(STEELJSON_CMAKE_DIR cmake)
set(STEELJSON_TARGET_NAME ${PROJECT_NAME})

set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${PROJECT_BINARY_DIR}/lib)
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${PROJECT_BINARY_DIR}/lib)
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${PROJECT_BINARY_DIR}/bin)

# set output directories for each build configuration
foreach(CONFIG ${CMAKE_CONFIGURATION_TYPES})
	string(TOUPPER ${CONFIG} UPPER_CONFIG)
	set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY_${UPPER_CONFIG} ${CMAKE_ARCHIVE_OUTPUT_DIRECTORY}/${CONFIG})
	set(CMAKE_LIBRARY_OUTPUT_DIRECTORY_${UPPER_CONFIG} ${CMAKE_LIBRARY_OUTPUT_DIRECTORY}/${CONFIG})
	set(CMAKE_RUNTIME_OUTPUT_DIRECTORY_${UPPER_CONFIG} ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/${CONFIG})
endforeach(CONFIG CMAKE_CONFIGURATION_TYPES)

add_subdirectory(examples)
add_subdirectory(src)
add_subdirectory(tests)

configure_file(
	${STEELJSON_CMAKE_DIR}/${STEELJSON_CONFIG_FILENAME}.in
	${STEELJSON_CMAKE_DIR}/${STEELJSON_CONFIG_FILENAME}
	@ONLY
)

include(CMakePackageConfigHelpers)
write_basic_package_version_file(${STEELJSON_CMAKE_DIR}/${STEELJSON_CONFIGVERSION_FILENAME}
	COMPATIBILITY SameMajorVersion
)

export(EXPORT ${STEELJSON_EXPORT_NAME} FILE ${STEELJSON_CMAKE_DIR}/${STEELJSON_EXPORT_NAME}.cmake)
export(PACKAGE ${STEELJSON_PACKAGE_NAME})

if(WIN32)
	install(FILES
		${PROJECT_BINARY_DIR}/${STEELJSON_CMAKE_DIR}/${STEELJSON_CONFIG_FILENAME}
		${PROJECT_BINARY_DIR}/${STEELJSON_CMAKE_DIR}/${STEELJSON_CONFIGVERSION_FILENAME}
		DESTINATION ${STEELJSON_CMAKE_DIR}
	)
	install(EXPORT ${STEELJSON_EXPORT_NAME}
		DESTINATION ${STEELJSON_CMAKE_DIR}
	)
else(WIN32)
	install(FILES
		${PROJECT_BINARY_DIR}/${STEELJSON_CMAKE_DIR}/${STEELJSON_CONFIG_FILENAME}
		${PROJECT_BINARY_DIR}/${STEELJSON_CMAKE_DIR}/${STEELJSON_CONFIGVERSION_FILENAME}
		DESTINATION lib/${STEELJSON_CMAKE_DIR}/${STEELJSON_PACKAGE_NAME}
	)
	install(EXPORT ${STEELJSON_EXPORT_NAME}
		DESTINATION lib/${STEELJSON_CMAKE_DIR}/${STEELJSON_PACKAGE_NAME}
	)
endif(WIN32)

install(DIRECTORY
	include
	DESTINATION .
)
